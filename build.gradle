buildscript {
     repositories {
         mavenCentral()
     }
 }

 allprojects {
     apply plugin: 'java'

     group 'com.paypal.checkout.sdk'
     version '1.0.0-RC2'
     sourceCompatibility = 1.8
     targetCompatibility = 1.8

     repositories {
         mavenCentral()
     }

     tasks.withType(Javadoc) {
         options.addStringOption('Xdoclint:none', '-quiet')
     }

     test {
         useTestNG {
             testLogging.showStandardStreams = true
             outputs.upToDateWhen { false }
         }
     }
 }
 project(':checkout-sdk') {
 	

    def braintreehttpVersion = "1.3.1"

	apply plugin: 'java'
	
	 repositories {
         mavenCentral()
     }
     
     dependencies {
         compile "com.braintreepayments:braintreehttp:${braintreehttpVersion}"
		 testCompile group: 'org.testng', name: 'testng', version: '6.9.13.6'
         testCompile "com.github.tomakehurst:wiremock:2.4.1"
         testCompile "com.braintreepayments:braintreehttp-testutils:${braintreehttpVersion}"

     }
     
	apply plugin: 'signing'

    signing {
        sign configurations.archives
    }
    
    apply plugin: 'maven'
    
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
    
     uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'checkout-sdk'
                groupId  'com.paypal.checkout.sdk'
                artifactId 'checkout-sdk'
                version '1.0.0-RC2'
                packaging 'jar'
                description 'PayPal SDK for integrating with the Checkout REST APIs'
                url 'https://github.com/paypal/Checkout-Java-SDK.git'

                scm {
                    url 'https://github.com/paypal/Checkout-Java-SDK.git'
                    connection 'scm:git:git://github.com/paypal/Checkout-Java-SDK.git'
                }

                licenses {
                    license {
                        name 'PayPal SDK License'
                        url 'https://github.com/paypal/Checkout-Java-SDK'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'DL-PP-JAVA-SDK'
                        name 'DL-PP-JAVA-SDK'
                        email 'dl-paypal-checkout-api@paypal.com'
                    }
                }
            }
        }
    }
    
}
 project("checkout-sdk-sample") {
     apply plugin: 'java'
     dependencies {
         compile "org.apache.commons:commons-lang3:3.6"
         compile group: 'org.json', name: 'json', version: '20180813'
         compile project(':checkout-sdk')

     }
 }
